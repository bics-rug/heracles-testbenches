FeCap hysteresis simulation

ground 0

// Generic devices...
load "resistor.osdi"
load "heracles_vacask.osdi"

// ... and their models (create masters)
model resistor resistor
model isource isource
model vsource vsource

// FeCap (Heracles) model
include "modelcard.lib" section=model

// Polarization hysteresis testbench
subckt polarization()
  vin (vin 0) vsource type="pulse" dc=0 mag=1 val0=-3 val1=3 delay=0 rise=500u fall=500u width=1n
  r1 (vin te) resistor r=50
  x1 (te 0) fecap
ends

// Capacitance hysteresis testbench
subckt capacitance()
  parameters vreset=-3 vset=1
  vpulse (vin_i 0) vsource type="pulse" dc=0 mag=1 val0=vreset val1=vset delay=1n rise=1n fall=1n width=1
  vsine (vin vin_i) vsource type="sine" dc=0 mag=1 delay=1m ampl=42m freq=10k
  r1 (vin te) resistor r=50
  x1 (te 0) fecap
ends

control
  abort always

  elaborate circuit("polarization")
  print hierarchy
  print nodes
  print unknowns
  
  save (
    default
  )

  analysis tran1 tran stop=1m step=1u


  elaborate circuit("capacitance")

  print hierarchy
  print nodes
  print unknowns
  save (
    default
  )
  analysis tran2 tran stop=1.2m step=1u maxstep=1u

  postprocess(PYTHON, "postproc.py")
endc
